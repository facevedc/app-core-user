plugins {
    id 'java'
    id 'jacoco'
    id 'com.github.spotbugs' version '4.6.0'
    id 'org.springframework.boot' version '3.2.4'
    id 'io.spring.dependency-management' version '1.1.4'
}

group = 'com.app.core.user'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '21'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

spotbugs {
    toolVersion = '4.8.4'
    excludeFilter = file('exclude-spotbugs.xml')
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
    implementation 'io.asyncer:r2dbc-mysql:1.1.0'

    implementation 'org.apache.commons:commons-pool2:2.12.0'

    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'

    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.12.0'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    'com/sentinel/app/core/user/setting/**',
                    'com/sentinel/app/core/user/infrastructure/**',
                    'com/sentinel/app/core/user/domain/**/model/**',
                    'com/sentinel/app/core/user/api/**/dto/**',
                    'com/sentinel/app/core/user/api/**/constants/**',
                    'com/sentinel/app/core/user/api/**/*Router.class',
                    'com/sentinel/app/core/user/api/ping/**',
                    'com/sentinel/app/core/user/*.class'
            ])
        }))
    }
}

jacoco {
    toolVersion = "0.8.10"
}
